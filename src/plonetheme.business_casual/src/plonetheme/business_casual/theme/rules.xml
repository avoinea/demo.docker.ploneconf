<?xml version="1.0" encoding="utf-8"?>
<rules xmlns="http://namespaces.plone.org/diazo"
    xmlns:css="http://namespaces.plone.org/diazo/css"
    xmlns:xhtml="http://www.w3.org/1999/xhtml"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform" >

  <theme href="index.html"/>
  <notheme if="$ajax_load" />

  <!-- Toolbar -->
  <before
      css:theme-children="body"
      css:content-children="#edit-bar"
      css:if-not-content=".ajax_load"
      css:if-content=".userrole-authenticated"
      />
  <replace
      css:theme="#anonymous-actions"
      css:content-children="#portal-personaltools-wrapper"
      css:if-not-content=".ajax_load"
      css:if-content=".userrole-anonymous"
      />
  <drop attributes="class" css:content="#edit-bar a.pat-plone-modal" />

  <notheme css:if-not-content="#visual-portal-wrapper" />

  <rules css:if-content="#portal-top">
    <!-- Attributes -->
    <copy attributes="*" css:theme="html" css:content="html" />
    <!-- Base tag -->
    <before css:theme="head title" css:content="head base" />
    <!-- Title -->
    <replace css:theme="head title" css:content="head title" />
    <!-- Pull in Plone Meta -->
    <after css:theme-children="head" css:content="head meta" />
    <!-- dont use Plone icons, use the theme's -->
    <drop css:content="head link[rel='apple-touch-icon'], head link[rel='shortcut icon']" />
    <!-- CSS -->
    <after css:theme-children="head" css:content="head link" />
    <after css:theme-children="head" css:content="head style" />
    <!-- Script -->
    <after css:theme-children="head" css:content="head script" />
  </rules>

  <!-- Copy over the id/class attributes on the body tag.
       This is important for per-section styling -->
  <copy attributes="*" css:content="body" css:theme="body" />

  <!-- Logo -->
  <replace css:theme=".brand-logo">
    <xsl:value-of select="$site_title"/>
  </replace>

  <!-- Slogan/address -->
  <replace css:theme=".brand-slogan">
    <xsl:value-of select="$site_slogan"/>
  </replace>

  <!-- Portal Navigation -->
  <replace
      css:content-children="#portal-globalnav"
      css:theme-children="#navbar"
      />

  <replace
      css:theme-children=".navbar-nav"
      css:content-children=".plone-navbar-nav"
      />

  <!-- dynamic slider content only for addon -->
  <replace
    if="$is_zip"
    css:theme="#slider"
    css:content="#slider"
    href="/slider-images/@@slider-images"
    />

  <!-- static banner for zip -->
  <replace css:theme="#site-banner" if-not="$is_zip">
    <div id="site-banner-img">
      <div class="box">
        <div class="col-lg-12 text center">
          <div class="banner"></div>
        </div>
      </div>
    </div>
  </replace>

  <!-- front-page wrap -->
  <drop
    css:theme="#slider"
    css:if-not-content=".section-front-page" />
  <drop
    css:theme="#slider"
    css:if-content=".template-edit" />
  <drop
    css:theme="#slider"
    css:if-content=".template-topbar-manage-portlets" />

  <drop
    css:theme="#site-banner"
    css:if-not-content=".section-front-page" />
  <drop
    css:theme="#site-banner"
    css:if-content=".template-edit" />
  <drop
    css:theme="#site-banner"
    css:if-content=".template-topbar-manage-portlets" />

  <drop
    css:content="#portal-breadcrumbs"
    css:if-content=".section-front-page" />

  <!-- Portal-searchbox -->
  <replace
      css:content='#portal-searchbox'
      css:theme='#portal-searchbox'
      />

  <replace css:content="div#portal-searchbox input.searchButton">
    <button class="search-icon" type="submit">
      <i class="icon-search"></i>
    </button>
  </replace>

  <replace css:content="div.mobile-live-search #portal-searchbox input.searchButton">
    <button class="search-icon">
      <i class="icon-search"></i>
    </button>
  </replace>

  <before css:content=".documentFirstHeading">
    <hr/>
  </before>

  <after css:content=".documentFirstHeading">
    <hr/>
  </after>

  <!-- login link -->
  <after
      css:theme-children=".navbar-nav"
      css:content="#portal-anontools"
      css:if-not-content=".ajax_load"
      css:if-content=".userrole-anonymous"
      />

  <rules css:if-content="#portal-anontools" >
    <strip css:content="div#portal-anontools ul" />
    <strip css:content="#portal-anontools" />
  </rules>

  <!-- Full-width breadcrumb -->
  <replace
      css:theme-children="#above-content"
      css:content-children="#viewlet-above-content"
      />

  <!-- Alert message -->
  <replace
      css:theme-children="#global_statusmessage"
      css:content-children="#global_statusmessage"
      />

  <!-- Central column -->
  <replace css:theme="#content-container" method="raw">
    <xsl:variable name="central">
      <xsl:if test="//aside[@id='portal-column-one'] and //aside[@id='portal-column-two']">col-xs-12 col-sm-6</xsl:if>
      <xsl:if test="//aside[@id='portal-column-two'] and not(//aside[@id='portal-column-one'])">col-xs-12 col-sm-9</xsl:if>
      <xsl:if test="//aside[@id='portal-column-one'] and not(//aside[@id='portal-column-two'])">col-xs-12 col-sm-9</xsl:if>
      <xsl:if test="not(//aside[@id='portal-column-one']) and not(//aside[@id='portal-column-two'])">col-xs-12 col-sm-12</xsl:if>
    </xsl:variable>

    <div class="{$central}">
      <div class="row">
        <div class="box">
          <div class="col-xs-12 col-sm-12">
            <xsl:apply-templates css:select="#content" />
          </div>
          <div class="clearFix"></div>
        </div>
      </div>
      <section id="viewlet-below-content-body" class="row">
        <div class="">
          <div class="col-xs-12 col-sm-12">
            <xsl:copy-of css:select="#viewlet-below-content" />
          </div>
          <div class="clearFix"></div>
        </div>
      </section>
    </div>
  </replace>

  <!-- Left column -->
  <rules css:if-content="#portal-column-one">
    <replace css:theme="#column1-container">
      <div class="col-xs-6 col-sm-3 sidebar-offcanvas">
        <aside id="portal-column-one">
          <xsl:copy-of css:select="#portal-column-one > *" />
        </aside>
      </div>
    </replace>
  </rules>

  <!-- Right column -->
  <rules css:if-content="#portal-column-two">
    <replace css:theme="#column2-container">
      <div class="col-xs-6 col-sm-3 sidebar-offcanvas" role="complementary">
        <aside id="portal-column-two">
          <xsl:copy-of css:select="#portal-column-two > *"/>
        </aside>
      </div>
    </replace>
  </rules>

  <!-- footer -->
  <replace
      css:theme-children="#portal-footer-wrapper"
      css:content-children="#portal-footer-wrapper"
      />

  <!-- add alert to invalid fields -->
  <xsl:template match="//div//@class[contains(., 'fieldErrorBox')]">
    <xsl:attribute name="class">
      <xsl:value-of select="." /> alert-danger
    </xsl:attribute>
  </xsl:template>

</rules>